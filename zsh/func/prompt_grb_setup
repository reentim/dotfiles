autoload -U zgitinit
zgitinit

coloratom() {
  local off=$1 atom=$2
  [[ $atom[1] == [[:upper:]] ]] && off=$(( $off + 60 ))
  echo $(( $off + $colorcode[${(L)atom}] ))
}

colorword() {
  local fg=$1 bg=$2 att=$3
  local -a s

  [[ -n "$fg" ]] && s+=$(coloratom 30 $fg)
  [[ -n "$bg" ]] && s+=$(coloratom 40 $bg)
  [[ -n "$att" ]] && s+=$attcode[$att]

  echo "%{"$'\e['${(j:;:)s}m"%}"
}

prompt_formatted_scm_info() {
  zgit_inworktree && echo "(%B$pc[scm_branch]$(prompt_scm_branch)%b$pc[reset])"
}

prompt_setup() {
  typeset -A colorcode
  typeset -A attcode
  local -A pc

  colorcode[black]=0
  colorcode[red]=1
  colorcode[green]=2
  colorcode[yellow]=3
  colorcode[blue]=4
  colorcode[magenta]=5
  colorcode[cyan]=6
  colorcode[white]=7
  colorcode[default]=9

  colorcode[k]=$colorcode[black]
  colorcode[r]=$colorcode[red]
  colorcode[g]=$colorcode[green]
  colorcode[y]=$colorcode[yellow]
  colorcode[b]=$colorcode[blue]
  colorcode[m]=$colorcode[magenta]
  colorcode[c]=$colorcode[cyan]
  colorcode[w]=$colorcode[white]
  colorcode[.]=$colorcode[default]

  attcode[none]=00
  attcode[bold]=01
  attcode[faint]=02
  attcode[standout]=03
  attcode[underline]=04
  attcode[blink]=05
  attcode[reverse]=07
  attcode[conceal]=08

  attcode[normal]=22
  attcode[no-standout]=23
  attcode[no-underline]=24
  attcode[no-blink]=25
  attcode[no-reverse]=27
  attcode[no-conceal]=28

  pc[default]='default'
  pc[time]='green'
  pc[user]='green'
  pc[punc]='yellow'
  pc[path]='cyan'
  pc[shortpath]='magenta'
  pc[return_code]='red'
  pc[#]='default'

  pc[scm_branch]='cyan'
  pc[scm_commitid]='yellow'
  pc[scm_status_dirty]='red'
  pc[scm_status_staged]='green'

  if [[ $(uname) == 'Darwin' ]]; then
    pc[host]='cyan'
  else
    pc[host]='green'
  fi

  for cn in ${(k)pc}; do
    pc[${cn}]=$(colorword $pc[$cn])
  done
  pc[reset]=$(colorword . . 00)

  typeset -Ag prompt_colors
  prompt_colors=(${(kv)pc})

  local p_time="$pc[time]%D{%T}$pc[reset]"

  precmd() {
    local ret="$?"
    [[ $ret != "0" ]] && print "=> $pc[return_code]$ret$pc[reset]"
  }

  PROMPT=""
  PROMPT+='%(1j.[%j] .)'
  [[ -n $SSH_CONNECTION ]] && PROMPT+="%B$pc[host]%m$pc[reset]%B|"
  PROMPT+="%B$pc[shortpath]%1~%b$pc[reset]"
  PROMPT+="\$(prompt_formatted_scm_info)"
  PROMPT+="$pc[#]%b"
  [[ -n $PROMPT_TIME ]] && PROMPT+="[$p_time]"
  PROMPT+="$pc[reset]%% "

  export PROMPT RPROMPT
}

prompt_scm_branch() {
  zgit_isgit || return
  local -A pc
  pc=(${(kv)prompt_colors})

  echo -n "$pc[punc]$pc[scm_branch]$(zgit_head)"

  if zgit_inworktree; then
    zgit_isindexclean || echo -n "$pc[default]|$pc[scm_status_staged]+"

    local -a dirty
    zgit_isworktreeclean || dirty+=+
    zgit_hasunmerged && dirty+=*
    zgit_hasuntracked && dirty+=?

    [[ $#dirty -gt 0 ]] && echo -n "$pc[default]|$pc[scm_status_dirty]${(j::)dirty}"
  fi

  echo $pc[reset]
}

prompt_setup "$@"

# vim:ft=zsh:
